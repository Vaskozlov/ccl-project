%PARSER%

GOAL:
    GLOBAL_DECLARATIONS

GLOBAL_DECLARATIONS:
    GLOBAL_DECLARATION GLOBAL_DECLARATIONS
    | GLOBAL_DECLARATION
    | EPSILON

GLOBAL_DECLARATION:
    FUNCTION_DEFINITION
    | PARSER_RULE
    | LEXER_RULE
    | VARIABLE_DECLARATION ';'
    | TYPE_VARIABLE_DECLARATION ';'

LEXER_RULE:
    "lexer" IDENTIFIER
    '{'
        LEXER_RULE_BODY
    '}'

LEXER_RULE_BODY:
    LEXER_RULE_BODY ',' EXPRESSION
    | EXPRESSION
    | EPSILON

PARSER_RULE:
    "parser" IDENTIFIER
    '{'
        PARSER_RULE_BODY
    '}'

PARSER_RULE_BODY:
    PARSER_SYMBOLS ';' PARSER_FUNCTIONS

PARSER_FUNCTIONS:
    PARSER_FUNCTIONS FUNCTION_DEFINITION
    | FUNCTION_DEFINITION
    | EPSILON

PARSER_SYMBOLS:
    PARSER_SYMBOLS ',' EXPRESSION
    | EXPRESSION
    | EPSILON

FUNCTION_DEFINITION:
    "def" IDENTIFIER '(' FUNCTION_DEFINITION_ARGUMENTS ')' FUNCTION_RETURN_TYPE
    '{'
        STATEMENTS
    '}'

FUNCTION_DEFINITION_ARGUMENTS:
    FUNCTION_DEFINITION_ARGUMENT ',' FUNCTION_DEFINITION_ARGUMENTS
    | FUNCTION_DEFINITION_ARGUMENT
    | EPSILON

FUNCTION_DEFINITION_ARGUMENT:
    TYPE IDENTIFIER

FUNCTION_RETURN_TYPE:
    '->' TYPE
    | EPSILON

STATEMENTS:
    STATEMENT STATEMENTS
    | STATEMENT
    | EPSILON

STATEMENT:
    EXPRESSION ';'
    | VARIABLE_DECLARATION ';'
    | TYPE_VARIABLE_DECLARATION ';'
    | FOR_LOOP
    | IF_STATEMENT
    | RETURN_STATEMENT ';'

RETURN_STATEMENT:
    "return" EXPRESSION
    | "return"

IF_STATEMENT:
    IF_BLOCK
    ELIF_BLOCK
    ELSE_BLOCK

IF_BLOCK:
    "if" EXPRESSION
    '{'
        STATEMENTS
    '}'

ELIF_BLOCK:
    ELIF_BLOCK
    "elif" EXPRESSION
    '{'
        STATEMENTS
    '}'
    | EPSILON

ELSE_BLOCK:
    "else"
    '{'
        STATEMENTS
    '}'
    | EPSILON

VARIABLE_DECLARATION:
    "var" IDENTIFIER '=' EXPRESSION

TYPE_VARIABLE_DECLARATION:
    "var" IDENTIFIER ':' TYPE '=' EXPRESSION

FOR_LOOP:
    "for" IDENTIFIER "in" EXPRESSION
    '{'
        STATEMENTS
    '}'

EXPRESSION:
    EXPRESSION '+' TERM
    | EXPRESSION '-' TERM
    | TERM

TERM:
    TERM '*' VALUE
    | TERM '/' VALUE
    | VALUE

VALUE:
    '-' VALUE
    | '+' VALUE
    | FACTOR

FUNCTION_CALL:
    IDENTIFIER '(' FUNCTION_CALL_ARGUMENTS ')'

FUNCTION_CALL_ARGUMENTS:
    EXPRESSION ',' FUNCTION_CALL_ARGUMENTS
    | EXPRESSION

METHOD_CALL:
    FACTOR '.' IDENTIFIER '(' FUNCTION_CALL_ARGUMENTS ')'

FACTOR:
    NUMBER
    | IDENTIFIER
    | STRING
    | FUNCTION_CALL
    | METHOD_CALL
    | '(' EXPRESSION ')'

TYPE:
    IDENTIFIER
    | IDENTIFIER '[' TYPE ']'

%LEXER%
IDENTIFIER: [a-zA-Z_] [a-zA-Z0-9_]*
NUMBER:     [0-9]+
STRING:     ! ["] ["]^* ["]
